(.) : a -> (a -> b) -> b forall a, b --
= fn
| a, f => f(a)
--

infixl 10 .

($) : (a -> b) -> a -> b forall a, b --
= fn
| f, a => f(a)
--

infixr 0 $

data a /\ b

infixl 3 /\

data a .. b

infixl 4 ..

pop = fn
| a .. b =>
    (a < b).fn
    | True => (a + 1)..b /\ a
    | False => ()
    --
| Filter(l, f) =>
    l.pop.fn
    | () => ()
    | l2 /\ a =>
        a.f.fn
        | True => Filter(l2, f) /\ a
        | False => Filter(l2, f).pop
        --
    --
--

all = fn
| i, f =>
    i.pop.fn
    | () => True
    | i2 /\ a =>
        a.f.fn
        | True => i2.all(f)
        | False => False
        --
    --
--

is_prime
= fn
| n =>
    (2..n - 1).all $ fn
    | i => n % i != 0
    --
--

each = fn
| l, f =>
    l.pop.fn
    | () => ()
    | l2 /\ a =>
        a.f
        l2.each(f)
    --
--

data Filter(a, b)

filter = fn
| l, f =>
    Filter(l, f)
--

print_num = fn
| n => n.num_to_string.println
--

main : () -> ()
= fn
| () =>
    (2..100).filter(is_prime).each(print_num)
--
